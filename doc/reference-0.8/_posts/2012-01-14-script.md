---
title: Scripts
layout: reference
permalink: /doc/reference/0.8/script
section: documentation
subsection: reference
summary: Pallet uses a script DSL called stevdeore to generate shell scripts
         that can target different distributions and operating systems.
prior: /doc/reference/0.8/parameters
follow: /doc/reference/0.8/logging
apiver: 0.8
---

Pallet scripts are written using an embedding of shell script in
[Clojure](http://clojure.org), and allows for abstraction of operating system
variations.

At it's simplest, the `pallet.stevedore/script` macro takes one or more forms
and converts them to shell script.

``` clojure
(require '[pallet.stevedore :refer [script with-script-language]])
(require 'pallet.stevedore.bash) ;; for bash output

(with-script-language :pallet.stevedore.bash/bash
  (script
    ("ls")))
```

results in:

``` bash
ls
```

The function position is resolved (see [Script functions](#script_functions)
below), so needs to be a string if you want to pass a literal.

For the ease of trying things at the REPL, the following examples allow you to
drop the `with-script-language` wrapping form. N.B. do not do this in your code!

``` clojure
(.bindRoot #'pallet.stevedore/*script-language* :pallet.stevedore.bash/bash)
```

## Variables

There is an obvious mismatch between Clojure's immutable data approach, and
shell script's mutable variables.

### Substitution

For accessing the value of a script variable, stevedore uses `deref` or `@`,
which are also used for command substitution.

``` clojure
(script (deref TMPDIR))
```

results in:

``` bash
${TMPDIR}
```

If the variable isn't set, a default can be supplied.
``` clojure
(script @TMPDIR-/tmp)
```

results in:

``` bash
${TMPDIR-/tmp}
```


### Assignment
Values can be defined with `set!`.
``` clojure
(script (set! x 1))
```

results in:

``` bash
x=1
```


## Command Substitution

Similar to variable substitution, command substitution uses `deref`.

``` clojure
(script (set! R @("cat" "/etc/redhat-release")))
```

results in:

``` bash
R=$(cat /etc/redhat-release)
```

## Loops
Shell script `for` loops are mapped to clojure's `doseq`.

``` clojure
(script
 (doseq [x ["a" "b" "c"]]
   (println @x)))
```

results in:

``` bash
for x in a b c; do
  echo ${x}
done
```

You can also combine command substitution with loops like this

``` clojure
(script
 (doseq [x @("ls" "/home/")]
   (println @x)))
```

``` bash
for x in $(ls /home/); do
  echo ${x}
done
```

## Functions

Calling a function follows clojure's normal syntax.

``` clojure
(script ("foo" x y))
```

 results in:

``` bash
foo x y
```

Defining a function follows a simplified version of clojure's `defn`, and does
not allow for destructuring. Arguments are named and automatically mapped to the
shell function's numbered arguments.

``` clojure
(script (defn foo [x y] ("bar" x)))
```

results in:

``` bash
script function foo() {
  x=$1
  y=$2
  bar x
}
```

## Arrays

Array literals are generated from clojure's `vector` notation.

``` clojure
(script [1 "2" (quoted 3) :four])
```

results in:
``` bash
(1 2 "3" four)
```

Array access uses `aget` and `aset`.

``` clojure
(script (aget foo 1))
```

results in:

``` bash
${foo[1]}
```

``` clojure
(script (aset foo 1 :foo))
```

results in:

``` bash
foo[1]=foo
```

## Unquoting

One advantage of embedding script in clojure is that we can escape back to
clojure to substitute in values.

``` clojure
(let [tmp "/tmp"]
  (script ("ls" ~tmp)))
```

results in:

``` bash
ls /tmp
```

### Expressions
We can also unquote arbitrary clojure expressions.
``` clojure
(script ("ls" ~(str "/" "tmp")))
```

results in:

``` bash
ls /tmp
```

### Splicing
Sometimes we want to add the contents of a collection into a script, without
demarking the boundaries of the collection.
``` clojure
(let [files ["a" "b" "c"]]
  (script ("ls" ~@files)))
```

results in:

``` bash
ls a b c
```

## Script functions

Script functions are defined with `pallet.script/defscript` and provide
multimethods for shell script generation.  In pallet this is used to abstract
away script differences between operating systems.

``` clojure
(require
 'pallet.stevedore.bash)
(use
 '[pallet.script :only [defscript defimpl with-script-context]]
 '[pallet.stevedore :only [script with-script-language]])

(defscript ls [& args])
(defimpl ls :default [& args]
  (ls ~@args))
(defimpl ls [:windows] [& args]
  (dir ~@args))

(with-script-language :pallet.stevedore.bash/bash
  (with-script-context [:windows]
    (script (ls a b c))))
```

results in:

``` bash
dir a b c
```

## Chaining

Stevedore offers a few forms for chaining commands:

``` clojure
(script (pipe ("ls") ("grep" bin))) ; ls | grep bin
(script (chain-and ("ls") ("pwd"))) ; ls && pwd
(script (chain-or ("ls") ("pwd"))) ; ls || pwd
```


## Scripting Guide

An excellent reference on shell scripting is available at
[http://mywiki.wooledge.org/BashGuide](http://mywiki.wooledge.org/BashGuide).
